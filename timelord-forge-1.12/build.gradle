buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

version = modVersion
group = modGroup
archivesBaseName = "$modBaseName-forge-1.12.2"

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade project(":timelord-common")
}

minecraft {
    version = "1.12.2-14.23.4.2747"
    mappings = project.mcpVersion
    runDir = "run"

    replaceIn "TimeLord.java"
    replace "@@MODID@@", project.modBaseName
    replace "@@VERSION@@", project.version
}

reobf {
    jar {
        extraLines "PK: org/slf4j ru/xunto/org/slf4j"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}